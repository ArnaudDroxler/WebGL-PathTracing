<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <title>WebGl Ray-Tracing</title>
        <link rel="stylesheet" href="./css/stylesheet.css">
            <script type="text/javascript" src="./js/gl-matrix.js"></script>
            <script type="text/javascript" src="./js/webgl-debug.js"></script>
            <script type="text/javascript" src="./js/rayTracing.js"></script>

            <!-- vertex shader -->
            <script id="shader-vs" type="x-shader/x-vertex">
                attribute vec3 aVertexPosition;
                uniform mat4 uPMatrix;

                void main(void) {
                    gl_Position = uPMatrix * vec4(aVertexPosition, 1.0);
                }
            </script>
                <!-- fragment shader -->
            <script id="shader-fs" type="x-shader/x-fragment">
                precision mediump float;
                const float pi = 3.14159265358;
                const float sphereRayon = 0.25;

                uniform mat3 uRepMatrix;
                uniform float uAlpha;
                uniform mat4 uMVMatrix;
                uniform float uBounce;

                vec3 uDir;
                vec3 uOri =  vec3(0.0,0.0,2.0);

                uniform vec3 uSpherePos;
                uniform vec3 uLightPos;

                const vec3 boxMin = vec3(-1.0, -1.0, -1.0);
                const vec3 boxMax = vec3(1.0, 1.0, 1.0);

                float intersectSphere( vec3 dir, vec3 origin, vec3 spherePosition, float sphereRayon) {
                    vec3 l = origin - spherePosition;
                    float a = dot(dir, dir);
                    float b = 2.0 * dot(l, dir);
                    float c = dot(l, l) - sphereRayon*sphereRayon;
                    float discriminant = b*b - 4.0*c*a;
                    if(discriminant > 0.0) {
                        float t = (-b-sqrt(discriminant))/(2.0*a);
                        if(t > 0.0){
                            return t;
                        }
                    }
                    return 10000.0;
                }

                float intersectionCube( vec3 dir, vec3 origin, vec3 cubeMin, vec3 cubeMax) {
                    vec3 tMin = (cubeMin - origin) / dir;
                    vec3 tMax = (cubeMax - origin) / dir;
                    vec3 t1 = min(tMin, tMax);
                    vec3 t2 = max(tMin, tMax);
                    float tNear = max(max(t1.x, t1.y), t1.z);
                    float tFar = min(min(t2.x, t2.y), t2.z);
                    return (tNear >= tFar) ? tNear : tFar;
                }

                vec3 normalForSphere(vec3 hit, vec3 spherePosition, float sphereRayon) {
                    return (hit - spherePosition) / sphereRayon;
                }

                vec3 normalForCube(vec3 hit, vec3 cubeMin, vec3 cubeMax) {
                    vec3 normal = vec3(0.0, 0.0, 0.0);
                    if(hit.x < cubeMin.x + 0.00001){
                        normal = vec3(-1.0, 0.0, 0.0);
                    }else if (hit.x > cubeMax.x - 0.00001){
                        normal = vec3(1.0, 0.0, 0.0);
                    }
                    else if(hit.y < cubeMin.y + 0.00001){
                        normal = vec3(0.0, -1.0, 0.0);
                    }else if (hit.y > cubeMax.y - 0.00001){
                        normal = vec3(0.0, 1.0, 0.0);
                    }
                    else if(hit.z < cubeMin.z + 0.00001){
                        normal = vec3(0.0, 0.0, -1.0);
                    }else if (hit.z > cubeMax.z - 0.00001){
                        normal = vec3(0.0, 0.0, 1.0);
                    }
                    return normal;
                }

                float shadow(vec3 origin, vec3 dir) {
                        float tSphere = intersectSphere( dir , origin, uSpherePos, sphereRayon);
                        float tLight = intersectSphere( dir , origin, uLightPos, 0.01);
                        if ( tSphere < tLight){
                            return 0.0;
                        }else{
                            return 1.0;
                        }
                }

                vec3 uniformSampleHemisphere(float r1,float r2) {
                    float sinTheta = sqrt(1.0 - r1 * r1);
                    float phi = pi * 2.0 * r2;
                    float x = sinTheta * cos(phi);
                    float z = sinTheta * sin(phi);
                    return vec3(x, r1, z);
                }

                float random(vec3 scale, float seed) {
                    return fract(sin(mod(dot(gl_FragCoord.xyz+seed, scale),pi)) * (43758.5453 + seed) );
                }

                vec3 getNextDir(vec3 normal, float seed){
                    vec3 Nt, Nb;
                    if(abs(normal.x) >  abs(normal.y)){
                    Nt = vec3(normal.z,0.0,-normal.x) / sqrt(normal.x * normal.x + normal.z * normal.z); ;
                    }else{
                    Nt = vec3(0.0,-normal.z,normal.y) / sqrt(normal.z * normal.z + normal.y * normal.y ); ;
                    }
                    Nb = cross(normal, Nt);
                    float r1 = random(vec3(13.9898, 78.233, 151.7182),seed);
                    float r2 = random(vec3(63.7264, 10.873, 623.6736),seed);
                    vec3 sampleRand = uniformSampleHemisphere(r1,r2);
                    return vec3( sampleRand.x * Nb.x + sampleRand.y * normal.x + sampleRand.z * Nt.x, sampleRand.x * Nb.y + sampleRand.y * normal.y + sampleRand.z * Nt.y, sampleRand.x * Nb.z + sampleRand.y * normal.z + sampleRand.z * Nt.z);
                }

                vec3 raytrace(vec3 dir, vec3 origin, float index){
                    vec3 color = vec3(0.0);
                    vec3 surfaceColor = vec3(0.9);
                    float distanceFromTheLight;
                    for(float i=1.0; i<7.0; i+=1.0){
                        if(i <= uBounce){
                        //On calcul tout les intersection posible avec les objects de la scene
                        float tBox = intersectionCube(dir,origin, boxMin, boxMax);
                        float tsphere = intersectSphere(dir,origin,uSpherePos,sphereRayon);

                        //On test si le quel est le point d'intersection visible
                        float t = 10000.0;
                        if (tBox < t){
                             t = tBox;
                        }
                        if(tsphere < t){
                            t = tsphere;
                        }

                        vec3 hit = origin + dir * t;

                        //On calcule la normal au point d'intersection
                        vec3 normal;
                        if (t == tBox) {
                               normal = -normalForCube(hit, boxMin, boxMax);
                               if (hit.x < boxMin.x + 0.00001){
                                    surfaceColor = vec3(0.2, 1.0, 0.2);
                               }
                               else if (hit.x > boxMax.x - 0.00001){
                                    surfaceColor = vec3(1.0, 0.1, 0.1);
                               }
                        }
                        if (t == tsphere){
                            normal = normalForSphere(hit, uSpherePos, sphereRayon);
                        }

                        //On calcul la distance entre le point et la lumire pour applique la Loi en carré inverse
                        vec3 lightToHit = normalize(uLightPos - hit);
                        if(i == 1.0){
                            distanceFromTheLight = length(uLightPos - hit);
                        }else{
                            distanceFromTheLight += length(origin - hit);
                        }

                        //On peut calculer les coefficient des couleurs, l'intensite lumineuse, le diffuse, et l'intensite de l'ombre
                        float lightIntensity = 7.0 / (4.0* pi * distanceFromTheLight);
                        float diffuse = max(0.0, dot(lightToHit, normal));
                        float shadowIntensity = shadow(hit + normal*0.00001, lightToHit);

                        color += surfaceColor  *  diffuse * shadowIntensity * lightIntensity * 1.0/i;

                        dir = getNextDir(normal,i+index);
                        origin=hit;
                        }else{
                            break;
                        }
                    }

                    return color;
                }

                void main(void) {
                    vec3 pixelScreen = uRepMatrix * vec3(gl_FragCoord.x,gl_FragCoord.y,1.0);
                    vec3 pixelCamera = pixelScreen * vec3(uAlpha,uAlpha,-1.0);
                    uDir = normalize(pixelCamera);
                    vec3 dir =  vec3(vec4(uDir,0)*uMVMatrix);
                    vec3 ori =  vec3(vec4(uOri,0)*uMVMatrix);
                    vec3 ray11 = raytrace(vec3(dir.x+0.001,dir.y+0.001,dir.z),ori,2.0);
                    vec3 ray10 = raytrace(vec3(dir.x+0.001,dir.y-0.001,dir.z),ori,3.0);
                    vec3 ray01 = raytrace(vec3(dir.x-0.001,dir.y+0.001,dir.z),ori,4.0);
                    vec3 ray00 = raytrace(vec3(dir.x-0.001,dir.y-0.001,dir.z),ori,5.0);
                    vec3 color = sqrt(( ray11*ray11 + ray10*ray10 + ray01*ray01 + ray00*ray00)/4.0);
                    gl_FragColor = vec4(color,1.0);
                }
            </script>
    </head>
    <body>
      <h1>WebGl Path Tracing</h1>
      <p>
          L’algorithme de Path Tracing permet de simuler les rebonds de la lumière a travers la scène. L’application permet plusieurs interactions avec la scène.
      <ul>
          <li>Choisir le nombre de rebonds de la source lumineuse.</li>
          <li>Sélectionne la sphère et la déplacer dans la scène.</li>
          <li>Faire tournée la camera dans la scène.</li>
      </ul>
      </p>

      <div id="main">
        <canvas id="glcanvas" width="512" height="512"></canvas>
        <div id="error"><noscript>Please enable JavaScript.</noscript></div>
        <h2>Interaction</h2>
          <h3><span id="bounceDisplay">0</span> Rebond</h3>
          <p>
              <input type="range" name="bounce" id="bounce" min="0" max="5" step="1" value="0" onchange="bounceValue(this.value)" >
          </p>
      </div>
  </body>

</html>