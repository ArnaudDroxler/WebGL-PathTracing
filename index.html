<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <title>WebGl Ray-Tracing</title>
        <link rel="stylesheet" href="./css/stylesheet.css">
            <script type="text/javascript" src="./js/gl-matrix.js"></script>
            <script type="text/javascript" src="./js/webgl-debug.js"></script>
            <script type="text/javascript" src="./js/webgl-utils.js"></script>
            <script type="text/javascript" src="./js/rayTracing.js"></script>
            <!-- vertex shader -->
            <script id="shader-vs" type="x-shader/x-vertex">
                attribute vec3 aVertexPosition;
                uniform mat4 uMVMatrix;
                uniform mat4 uPMatrix;
                void main(void) {
                    gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
                }
            </script>
                <!-- fragment shader -->
            <script id="shader-fs" type="x-shader/x-fragment">
                precision mediump float;

                uniform mat3 uRepMatrix;
                uniform float uAlpha;

                vec3 uDir;
                vec3 uOri = vec3(0.0,0.0,2.0);

                float uSphereRayon = 0.25;
                vec3 uSpherePos = vec3(0.0,0.0,0.0);

                vec3 uBoxMin = vec3(-1.0, -1.0, -1.0);
                vec3 uBoxMax = vec3(1.0, 1.0, 1.0);

                vec3 uLightPos = vec3(0.4, 0.8, 0.6);

                vec3 normalForSphere(vec3 hit, vec3 spherePosition, float sphereRayon) {
                    return (hit - spherePosition) / sphereRayon;
                }

                float intersectSphere( vec3 dir, vec3 origin, vec3 spherePosition, float sphereRayon) {
                    vec3 l = origin - spherePosition;
                    float b = 2.0 * dot(l, dir);
                    float c = dot(l, l) - sphereRayon*sphereRayon;
                    float discriminant = b*b - 4.0*c;
                    if(discriminant > 0.0) {
                        float t = (-b-sqrt(discriminant))/2.0;
                        if(t > 0.0){
                            return t;
                        }
                    }
                    return 1000.0;
                }

                float intersectionCube( vec3 dir, vec3 origin, vec3 cubeMin, vec3 cubeMax) {
                    vec3 tMin = (cubeMin - origin) / dir;
                    vec3 tMax = (cubeMax - origin) / dir;
                    vec3 t1 = min(tMin, tMax);
                    vec3 t2 = max(tMin, tMax);
                    float tNear = max(max(t1.x, t1.y), t1.z);
                    float tFar = min(min(t2.x, t2.y), t2.z);
                    return (tNear >= tFar) ? tNear : tFar;
                }

                vec3 normalForCube(vec3 hit, vec3 cubeMin, vec3 cubeMax) {
                    vec3 normal = vec3(0.0, 0.0, 0.0);
                    if(hit.x < cubeMin.x + 0.00001){
                        normal = vec3(-1.0, 0.0, 0.0);
                    }else if (hit.x > cubeMax.x - 0.00001){
                        normal = vec3(1.0, 0.0, 0.0);
                    }
                    else if(hit.y < cubeMin.y + 0.00001){
                        normal = vec3(0.0, -1.0, 0.0);
                    }else if (hit.y > cubeMax.y - 0.00001){
                        normal = vec3(0.0, 1.0, 0.0);
                    }
                    else if(hit.z < cubeMin.z + 0.00001){
                        normal = vec3(0.0, 0.0, -1.0);
                    }else if (hit.z > cubeMax.z - 0.00001){
                        normal = vec3(0.0, 0.0, 1.0);
                    }
                    return normal;
                }

                float shadow(vec3 origin, vec3 dir) {
                    float tSphere = intersectSphere(dir, origin, uSpherePos, uSphereRayon);
                    if (tSphere < 1.0){
                        return 0.0;
                    }else{
                        return 1.0;
                    }

                }

                vec3 raytrace(vec3 dir, vec3 origin){
                    vec3 aColor = vec3(0.0,0.0,0.0);

                    //On calcul tout les intersection posible avec les objects de la scene
                    float tBox = intersectionCube(dir,origin, uBoxMin, uBoxMax);
                    float tsphere = intersectSphere(dir,origin,uSpherePos,uSphereRayon);

                    //On test si le quel est le point d'intersection visible
                    float t = 1000.0;
                    if (tBox < t){
                         t = tBox;
                    }
                    if(tsphere < t){
                        t = tsphere;
                    }


                    vec3 hit = origin + dir * t;
                    vec3 surfaceColor = vec3(1.0);

                    //On calcule la normal au point d'intersection
                    vec3 normal;
                    if (t == tBox) {
                           normal = -normalForCube(hit, uBoxMin, uBoxMax);
                           if (hit.x < uBoxMin.x + 0.00001) surfaceColor = vec3(0.2, 1.0, 0.2);
                           else if (hit.x > uBoxMax.x - 0.00001) surfaceColor = vec3(1.0, 0.1, 0.1);
                    }
                    if (t == tsphere){
                        normal = normalForSphere(hit, uSpherePos, uSphereRayon);
                    }

                    vec3 lightToHit = uLightPos - hit;
                    float diffuse = max(0.0, dot(normalize(lightToHit), normal));
                    float shadowIntensity = shadow(hit, normalize(lightToHit)+ normal*0.00001);
                    aColor += surfaceColor *  diffuse * shadowIntensity * 0.8;
                    return aColor;
                }

                void main(void) {
                    vec3 pixelScreen = uRepMatrix*vec3(gl_FragCoord.x,gl_FragCoord.y,1.0);
                    vec3 pixelCamera = pixelScreen*vec3(uAlpha,uAlpha,-1.0);
                    uDir = normalize(pixelCamera);
                    vec3 color = raytrace(uDir,uOri);
                    gl_FragColor = vec4(color,1.0);
                }
            </script>
    </head>
    <body>
      <h1>WebGl Ray-Tracing</h1>
      <p>
        Canvas pour cr√©er une application WebGL.
      </p>
      <div id="main">
        <canvas id="glcanvas" width="512" height="512"></canvas>
        <div id="error"><noscript>Please enable JavaScript.</noscript></div>
        <h2>Interaction</h2>
          <p>
            <button type="button" name="button" onclick="">Click Me!</button>
          </p>
      </div>
  </body>
</html>
